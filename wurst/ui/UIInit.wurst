package UIInit
import ClosureTimers
import UIFrame
import Fx
import UIResolution
import UIHandles
import UIChat
import Orders
import PlayerData


LinkedList<UIFrame> frames = null
framehandle array[12] cmdButs
framehandle array[12] cmdButsMovable
let bottomUI = consoleFrame.getChild(1)
let unitInfo = bottomUI.getChild(0)
let cmdButsPos = vec2(311, 1001)
let cmdButStride = vec2(387 - 311., 0)
let idleBut = consoleFrame.getChild(7).getChild(0)..clearAllPoints()
let confirmFx = "UI\\Feedback\\Confirmation\\Confirmation.mdl"

function onKeyDown(oskeytype key, OSKEY_META meta, code c)
	let t = CreateTrigger()
	for p from allPlayers.staticItr()
		t.registerPlayerKeyPress(p.p, key, meta, true)
	t.addAction(c)

function onKeyDown(oskeytype key, code c)
	onKeyDown(key, OSKEY_META.NONE, c)

function updateFrames()
	idleBut..clearAllPoints()
	..setAbsPoint(FRAMEPOINT_TOPLEFT, (cmdButsPos + cmdButStride * (isBuilder(localPlayer) ? 12 : 4) + vec2(10, 0)).toUiCoords())

	unitMsgFrame..clearAllPoints()
	..setAbsPoint(FRAMEPOINT_BOTTOMLEFT, vec2(350, 700).toUiCoords())

	let mmBottomleft = vec2(1647, 820 + 256.).toUiCoords()
	minimapFrame..clearAllPoints()
	..setAbsPoint(FRAMEPOINT_BOTTOMLEFT, mmBottomleft)
	..setAbsPoint(FRAMEPOINT_TOPRIGHT, mmBottomleft + vec2(256, 256).toUiSize())
	
	let namePos = vec2(392 + 157/2., 911).toUiCoords()
	bottomUI..clearAllPoints()..setAbsPoint(FRAMEPOINT_TOP, namePos)

	updateChatPositions()

	for f from frames.staticItr()
		f.update()
						
function string.repeat(int times) returns string
	var result = ""
	for _ = 0 to times
		result += this
	return result

vec2 array mousePos
Fx array orderFx

function framehandle.resetText()
	this.setText(this.getText())

public function initUI()
	getFrame("SimpleInventoryBar").setParent(getFrame("CommandBarFrame"))
	hideOriginFrames()
	minimapFrame..setVisible(true)..setParent(getFrame("ConsoleUIBackdrop"))
	getOriginFrame(ORIGIN_FRAME_HERO_BAR).setVisible(true)

	// resource bar kept visible for gold /fps /ping /apm
	getFrame("ResourceBarFrame")..setVisible(true)..clearAllPoints()..setAbsPoint(FRAMEPOINT_TOPRIGHT, 0.8, 0.63)
	
	for i = 0 to 11
		cmdButs[i] = getOriginFrame(ORIGIN_FRAME_COMMAND_BUTTON, i)
		cmdButsMovable[i] = getFrame("CommandButton_" + i.toString())..clearAllPoints()

	let statTextX = 311 + 323 / 2.
	let hpTextY = 943 + 27 / 2.
	let mpTextY = 970 + 27 / 2.
	frames = asList(
		new UIFrame(1633, 777, 284, 303, "Textures\\UI\\minimap.blp"),
		new UIFrame(10, 873, 634, 207, "Textures\\UI\\command.blp"),
		new UIFrame(23, 876, 57, 57, () -> getFrame("InventoryButton_0")),
		new UIFrame(82, 876, 57, 57, () -> getFrame("InventoryButton_1")),
		new UIFrame(23, 935, 57, 57, () -> getFrame("InventoryButton_2")),
		new UIFrame(82, 935, 57, 57, () -> getFrame("InventoryButton_3")),
		new UIFrame(23, 994, 57, 57, () -> getFrame("InventoryButton_4")),
		new UIFrame(82, 994, 57, 57, () -> getFrame("InventoryButton_5")),
		new UIFrame(58, 1054, 81, 24, () -> getFrame("ResourceBarGoldText")..setTextColor(colorA(214, 191, 68, 255))
			..setTextAlignment(TEXT_JUSTIFY_MIDDLE, TEXT_JUSTIFY_LEFT)..resetText()),
		new UIFrame(177, 935, 115, 115, () -> getOriginFrame(ORIGIN_FRAME_PORTRAIT)..setVisible(true)),
		new UIFrame(298, 943, 339, 27, "Textures\\UI\\hpBar.blp", 0)..setLevel(1),
		new UIFrame(311, 970, 323, 27, "Textures\\UI\\mpBar.blp", 0)..setLevel(1),
		new UIFrame(statTextX - 100, hpTextY - 15, 200, 30, TEXT_JUSTIFY_MIDDLE, TEXT_JUSTIFY_CENTER)..setLevel(2),
		new UIFrame(statTextX - 100, mpTextY - 15, 200, 30, TEXT_JUSTIFY_MIDDLE, TEXT_JUSTIFY_CENTER)..setLevel(2),
		new UIFrame(1756, 781, 35, 35, () -> createSimpleFrame("questsBut")..setLevel(1))
			..onClick(() -> getFrame("UpperButtonBarFrame").getChild(0).click()),
		new UIFrame(1793, 781, 35, 35, () -> createSimpleFrame("menuBut")..setLevel(1))
			..onClick(() -> getFrame("UpperButtonBarFrame").getChild(1).click()),
		new UIFrame(1830, 781, 35, 35, () -> createSimpleFrame("alliesBut")..setLevel(1))
			..onClick(() -> getFrame("UpperButtonBarFrame").getChild(2).click()),
		new UIFrame(1867, 781, 35, 35, () -> createSimpleFrame("chatBut")..setLevel(1))
			..onClick(() -> getFrame("UpperButtonBarFrame").getChild(3).click()))

	bottomUI..setVisible(true)..walk() (_, fh) ->
		if fh != bottomUI and fh != unitInfo
			fh..setAlpha(0)..moveAway()
	getFrame("InventoryText").moveAway()
	getOriginFrame(ORIGIN_FRAME_CHAT_MSG).setAlpha(0)

	EventListener.add(EVENT_PLAYER_CHAT_FILTER) ->
		let msg = GetEventPlayerChatString()
		if not msg.startsWith("-")
			GetTriggerPlayer().getData().sendChatMessage(msg)

	updateFrames()
	onResolutionChange((w, h) -> updateFrames())

	// command buttons & stat bars
	let hpFh = frames.get(10).fh
	let mpFh = frames.get(11).fh
	let hpStrFh = frames.get(12)
	let mpStrFh = frames.get(13)
	doPeriodically(1. / 32) _ ->
		var pos = cmdButsPos.toUiCoords()
		let stride = cmdButStride.toUiSize()
		for i = 0 to 11
			if cmdButs[i].isVisible()
				cmdButsMovable[i].setAbsPoint(FRAMEPOINT_TOPLEFT, pos)
				pos += stride
		if hpTextFrame != null and unitInfo.isVisible()
			let hpStr = hpTextFrame.getText()
			let mpStr = mpTextFrame.getText()
			hpStrFh.setText(hpStr)
			mpStrFh.setText(mpStr)
			hpFh.setValue(hpStr.readStat().ratio() * 100)
			mpFh.setValue(mpStr.readStat().ratio() * 100)
		else
			hpStrFh.setText("")
			mpStrFh.setText("")
			hpFh.setValue(0)
			mpFh.setValue(0)
	
	// stop hidden -> replicate
	onKeyDown(OSKEY_S) ->
		let p = GetTriggerPlayer()
		forUnitsSelected(p) u ->
			if u.getOwner() == p
				u.issueImmediateOrderById(OrderIds.stop)
	
	// patrol hidden -> replicate
	EventListener.add(EVENT_PLAYER_MOUSE_MOVE) ->
		mousePos[GetTriggerPlayer().getId()] = EventData.getMouseWorldPos()
	for pd in allPlayers
		orderFx[pd.id] = null
	onKeyDown(OSKEY_P) ->
		let p = GetTriggerPlayer()
		let pid = p.getId()
		let pos = mousePos[pid]
		var doFx = true
		forUnitsSelected(p) u ->
			if u.getOwner() == p
				if doFx
					doFx = false
					if orderFx[pid] != null
						orderFx[pid].hiddenDestroy()
					orderFx[pid] = new Fx(pos, localPlayer == p ? confirmFx : "")..setColor(colorA(0, 255, 0, 255))
				u.issuePointOrderById(OrderIds.patrol, pos)

init
	if not loadTOCFile("customUI.toc")
		error("loading customUI.toc failed")
	enableUIAutoPosition(false)
	consoleFrame.setAbsPoint(FRAMEPOINT_BOTTOM, 0.4, -0.18) // map crashes if done post-init
